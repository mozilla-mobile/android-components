// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    // Synchronized version numbers for dependencies used by (some) modules
    ext.dependencies = [
        kotlin: '1.2.41',
        coroutines: '0.22.5',
        supportLibraries: '27.1.1',
        // Test only:
        junit: '4.12',
        robolectric: '3.8',
        mockito: '2.18.0',
        mockwebserver: '3.7.0',
        dokka: '0.9.16'
    ]

    // Synchronized build configuration for all modules
    ext.build = [
        compileSdkVersion: 27,
        targetSdkVersion: 27,
        minSdkVersion: 21
    ]

    // Synchronized library configuration for all modules
    ext.library = [
        version: '0.12'
    ]

    // Synchronized versions numbers of GeckoView (Nightly) artifacts.
    ext.geckoNightly = [
        // Discover nightly builds: https://tools.taskcluster.net/index/gecko.v2.mozilla-central.nightly
        nightlyDate: '2018.06.21',
        revision: 'e834d23a292972ab4250a8be00e6740c43e41db2',
        version: '62.0.20180621100051'
    ]

    // Synchronized versions numbers of GeckoView (Beta) artifacts.
    ext.geckoBeta = [
        // (61.0b14)
        revision: '585281dde9604ddd197693ca5330f2b95987a181',
        version: '61.0.20180614135649'
    ]

    ext.geckoRelease = [
        revision: 'a0b222c551f586904f51228c49149d9b6b7e2a81',
        version: '60.0.2'
    ]

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${project.ext.dependencies['kotlin']}"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:${project.ext.dependencies['dokka']}"

        // Publish.
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.0.RC6-4")
}

allprojects {
    repositories {
        google()
        jcenter()

        ////////////////////////////////////////////////////////////////////////////////////////////
        // GeckoView Nightly
        ////////////////////////////////////////////////////////////////////////////////////////////

        // ARM
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-central.nightly.${geckoNightly['nightlyDate']}.revision.${geckoNightly['revision']}.mobile.android-api-16-opt/artifacts/public/android/maven"
        }

        // x86
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-central.nightly.${geckoNightly['nightlyDate']}.revision.${geckoNightly['revision']}.mobile.android-x86-opt/artifacts/public/android/maven"
        }

        // aarch64
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-central.nightly.${geckoNightly['nightlyDate']}.revision.${geckoNightly['revision']}.mobile.android-aarch64-opt/artifacts/public/android/maven"
        }

        ////////////////////////////////////////////////////////////////////////////////////////////
        // GeckoView Beta
        ////////////////////////////////////////////////////////////////////////////////////////////

        // ARM
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-beta.revision.${geckoBeta['revision']}.mobile.android-api-16-opt/artifacts/public/android/maven"
        }

        // x86
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-beta.revision.${geckoBeta['revision']}.mobile.android-x86-opt/artifacts/public/android/maven"
        }

        // aarch64
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-beta.revision.${geckoBeta['revision']}.mobile.android-aarch64-opt/artifacts/public/android/maven"
        }

        ////////////////////////////////////////////////////////////////////////////////////////////
        // GeckoView Release (60.0.2)
        ////////////////////////////////////////////////////////////////////////////////////////////

        // ARM
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-release.revision.${geckoRelease['revision']}.mobile.android-api-16-opt/artifacts/public/android/maven"
        }

        // x86
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-release.revision.${geckoRelease['revision']}.mobile.android-x86-opt/artifacts/public/android/maven"
        }

        // aarch64
        maven {
            url "https://index.taskcluster.net/v1/task/gecko.v2.mozilla-release.revision.${geckoRelease['revision']}.mobile.android-aarch64-opt/artifacts/public/android/maven"
        }
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.dokka-android'

    afterEvaluate {
        if (it.hasProperty('android')) {
            android {
                testOptions {
                    unitTests {
                        includeAndroidResources = true
                    }
                }

                task docs(type: org.jetbrains.dokka.gradle.DokkaAndroidTask) {
                    outputFormat = 'javadoc'
                    reportUndocumented = false
                    outputDirectory = "${project.rootDir}/build/javadoc/${project.name}"
                }
            }

            if (project.hasProperty("coverage")) {
                tasks.withType(Test) {
                    jacoco.includeNoLocationClasses = true
                    doLast { jacocoTestReport.execute() }
                }

                task jacocoTestReport(type: JacocoReport) {
                    reports {
                        xml.enabled = true
                        html.enabled = true
                    }

                    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                                      '**/*Test*.*', 'android/**/*.*', '**/*$[0-9].*']
                    def kotlinDebugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
                    def javaDebugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
                    def mainSrc = "$project.projectDir/src/main/java"

                    sourceDirectories = files([mainSrc])
                    classDirectories = files([kotlinDebugTree, javaDebugTree])
                    executionData = fileTree(dir: project.buildDir, includes: [
                            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
                    ])
                }

                android {
                    buildTypes {
                        debug {
                            testCoverageEnabled true
                        }
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    // The version number is duplicated, please refer to plugins block for more details
    version = "1.0.0.RC6-4"
    profile("main") {
        input = "$projectDir/components"
        config = "$projectDir/config/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/build/reports/detekt"
        baseline = "$projectDir/config/detekt-baseline.xml"
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.21.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "components/**/*.kt"
}

task printModules {
    doLast {
        subprojects.each { p ->
            println "mobule: " + p.path
        }
    }
}
