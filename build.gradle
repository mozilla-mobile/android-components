// Top-level build file where you can add configuration options common to all sub-projects/modules.

import org.gradle.internal.logging.text.StyledTextOutput.Style
import org.gradle.internal.logging.text.StyledTextOutputFactory
import static org.gradle.api.tasks.testing.TestResult.ResultType

buildscript {
    repositories {
        if (project.hasProperty("googleRepo")) {
            maven {
                name "Google"
                url project.property("googleRepo")
            }
        } else {
            google()
        }

        if (project.hasProperty("centralRepo")) {
            maven {
                name "MavenCentral"
                url project.property("centralRepo")
            }
        } else {
            mavenCentral()
        }

        if (project.hasProperty("jcenterRepo")) {
            maven {
                name "BintrayJCenter"
                url project.property("jcenterRepo")
            }
        } else {
            jcenter()
        }
    }

    dependencies {
        classpath Dependencies.tools_androidgradle
        classpath Dependencies.tools_kotlingradle
        classpath Dependencies.tools_dokka
        classpath Dependencies.tools_androidmavenpublish
    }
}

plugins {
    id("io.gitlab.arturbosch.detekt").version("1.0.1")  // Check version number in `Versions.detekt`
}

allprojects {
    repositories {
        if (project.hasProperty("googleRepo")) {
            maven {
                name "Google"
                url project.property("googleRepo")
            }
        } else {
            google()
        }

        if (project.hasProperty("centralRepo")) {
            maven {
                name "MavenCentral"
                url project.property("centralRepo")
            }
        } else {
            mavenCentral()
        }

        if (project.hasProperty("jcenterRepo")) {
            maven {
                name "BintrayJCenter"
                url project.property("jcenterRepo")
            }
        } else {
            jcenter()
        }

        maven {
            name "Mozilla"
            url "https://maven.mozilla.org/maven2"
        }
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.dokka-android'

    // Enable Kotlin warnings as errors for all modules
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions.allWarningsAsErrors = true
    }

    project.configurations.all {
        // Robolectric, through `com.google.android.apps.common.testing.accessibility.framework`
        // depends on an old version of protobuf that conflict with the Application Services one.
        // See:
        // - https://github.com/mozilla/application-services/issues/2952
        // - https://github.com/google/tink/issues/282
        // - https://github.com/robolectric/robolectric/issues/2643
        // The good news is the next version of Robolectric will not depend on this lib anymore.
        // (https://github.com/robolectric/robolectric/pull/5547)
        all*.exclude group: 'com.google.protobuf', module: 'protobuf-java'
        // Enforce that all (transitive) dependencies are using the same support library version as we do.
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && !details.requested.name.contains('multidex') ) {
                details.useVersion Versions.support_libraries
            }
        }
    }

    // Allow local appservices substitution in each subproject.
    if (gradle.hasProperty('localProperties.autoPublish.application-services.dir')) {
        ext.appServicesSrcDir = gradle."localProperties.autoPublish.application-services.dir"
        apply from: "${rootProject.projectDir}/${appServicesSrcDir}/build-scripts/substitute-local-appservices.gradle"
    }

    afterEvaluate {
        if (it.hasProperty('android')) {
            jacoco {
                toolVersion = "0.8.5"
            }

            // Format test output
            tasks.matching {it instanceof Test}.all {
                systemProperty "robolectric.logging", "stdout"
                systemProperty "logging.test-mode", "true"

                testLogging.events = []

                def out = services.get(StyledTextOutputFactory).create("an-ouput")

                beforeSuite { descriptor ->
                    if (descriptor.getClassName() != null) {
                        out.style(Style.Header).println("\nSUITE: " + descriptor.getClassName())
                    }
                }

                beforeTest { descriptor ->
                    out.style(Style.Description).println("  TEST: " + descriptor.getName())
                }

                onOutput { descriptor, event ->
                    logger.lifecycle("    " + event.message.trim())
                }

                afterTest { descriptor, result ->
                    switch (result.getResultType()) {
                        case ResultType.SUCCESS:
                            out.style(Style.Success).println("  SUCCESS")
                            break

                        case ResultType.FAILURE:
                            out.style(Style.Failure).println("  FAILURE")
                            logger.lifecycle("", result.getException())
                            break

                        case ResultType.SKIPPED:
                            out.style(Style.Info).println("  SKIPPED")
                            break
                    }
                    logger.lifecycle("")
                }
            }

            dependencies {
                lintChecks project(':tooling-lint')
            }

            android {
                testOptions {
                    unitTests {
                        includeAndroidResources = true
                    }
                }

                lintOptions {
                    warningsAsErrors true
                    abortOnError (project.name != "support-test")

                            // With our L10N process its totally possible to have missing or (temporarily) extra translations.
                    disable 'MissingTranslation',
                            'ExtraTranslation',
                            // We do not want to enforce this as a generic rule for all languages (see #6117, #6056, #6118)
                            'TypographyEllipsis'
                }

                packagingOptions {
                    exclude 'META-INF/atomicfu.kotlin_module'
                }

                compileOptions {
                    sourceCompatibility 1.8
                    targetCompatibility 1.8
                }

                tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
                    kotlinOptions {
                        jvmTarget = "1.8"
                    }
                }
            }

            if (project.hasProperty("coverage") && project.name != "support-test") {
                tasks.withType(Test) {
                    jacoco.includeNoLocationClasses = true

                    finalizedBy jacocoTestReport
                }

                task jacocoTestReport(type: JacocoReport) {
                    reports {
                        xml.enabled = true
                        html.enabled = true
                    }

                    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                                      '**/*Test*.*', 'android/**/*.*', '**/*$[0-9].*']
                    def kotlinDebugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
                    def javaDebugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
                    def mainSrc = "$project.projectDir/src/main/java"

                    sourceDirectories = files([mainSrc])
                    classDirectories = files([kotlinDebugTree, javaDebugTree])
                    executionData = fileTree(dir: project.buildDir, includes: [
                            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
                    ])
                }

                android {
                    buildTypes {
                        debug {
                            testCoverageEnabled true
                        }
                    }
                }
            }
        }
    }
}

evaluationDependsOnChildren()

task docs(type: org.jetbrains.dokka.gradle.DokkaAndroidTask, overwrite: true) {
    moduleName = "$rootProject.name"
    outputDirectory = "$buildDir/javadoc"
    outputFormat = "gfm"
    jdkVersion = 7

    subprojects.findAll {
        it.hasProperty("android") && !it.name.startsWith("samples")
    }.each {
        classpath += files(it.android.getBootClasspath().join(File.pathSeparator))
        it.android.libraryVariants.all { variant ->
            classpath += files(variant.javaCompile.classpath.files)
        }
    }

    externalDocumentationLink {
        url = new URL("https://mozilla.github.io/geckoview/javadoc/mozilla-central/package-list")
    }

    sourceDirs = files(subprojects.findAll {
        !it.name.startsWith("samples")
    }.collect { p ->
        def path = new File(p.projectDir, "/src/main/java")

        def relativePath = rootDir.toPath().relativize(path.toPath()).toString()
        linkMapping {
            dir = path
            url = "https://github.com/mozilla-mobile/android-components/blob/master/$relativePath"
            suffix = "#L"
        }

        return path
    })
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

detekt {
    input = files("$projectDir/components", "$projectDir/buildSrc", "$projectDir/samples")
    config = files("$projectDir/config/detekt.yml")
    baseline = file("$projectDir/config/detekt-baseline.xml")

    reports {
        html {
            enabled = true
            destination = file("$projectDir/build/reports/detekt.html")
        }
        xml {
            enabled = false
        }
    }
}

tasks.withType(io.gitlab.arturbosch.detekt.Detekt) {
    // Custom detekt rules should be build before
    // See https://arturbosch.github.io/detekt/extensions.html#pitfalls
    dependsOn(":tooling-detekt:assemble")

    autoCorrect = true

    exclude "**/src/androidTest/**"
    exclude "**/src/test/**"
    exclude "**/test/src/**"
    exclude "**/build/**"
    exclude "**/resources/**"
    exclude "**/tmp/**"
    exclude "**/tooling/fetch/tests/**"
    exclude "**/tooling/fetch-tests/**"
}

// Apply same path exclusions as for the main task
tasks.withType(io.gitlab.arturbosch.detekt.DetektCreateBaselineTask) {
    exclude "**/src/androidTest/**"
    exclude "**/src/test/**"
    exclude "**/test/src/**"
    exclude "**/build/**"
    exclude "**/resources/**"
    exclude "**/tmp/**"
    exclude "**/tooling/fetch/tests/**"
    exclude "**/tooling/fetch-tests/**"
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.31.0"
    detektPlugins project(":tooling-detekt")
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "components/**/*.kt" , "samples/**/*.kt", "!**/build", "buildSrc/**/*.kt"
}

apply plugin: GVVersionVerifierPlugin
apply plugin: GitHubPlugin


task listRepositories {
    doLast {
        println "Repositories:"
        project.repositories.each { println "Name: " + it.name + "; url: " + it.url }
   }
}
