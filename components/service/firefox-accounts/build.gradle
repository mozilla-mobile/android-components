/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.build['compileSdkVersion']

    defaultConfig {
        minSdkVersion rootProject.ext.build['minSdkVersion']
        targetSdkVersion rootProject.ext.build['targetSdkVersion']
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    ivy {
        url 'https://index.taskcluster.net/v1/task/'
        layout 'pattern', {
            artifact '/project.fxaclient.builds.[revision]/artifacts/public/bin/[organisation]/[module]_[revision].[ext]'
        }
    }
}

configurations{
    jni
}

dependencies {
    jni "mozilla:fxa_client_android:${rootProject.ext.dependencies['fxa']}@zip"
    jni "mozilla:fxa_client_android_deps:${rootProject.ext.dependencies['fxa']}@zip"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${rootProject.ext.dependencies['kotlin']}"
    implementation "net.java.dev.jna:jna:${rootProject.ext.dependencies['jna']}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${rootProject.ext.dependencies['coroutines']}"

    testImplementation "junit:junit:${rootProject.ext.dependencies['junit']}"
    testImplementation "org.robolectric:robolectric:${rootProject.ext.dependencies['robolectric']}"
}

task unzipJniLibs(type: Copy) {
    def outputDir = "$project.projectDir/src/main/jniLibs"
    from configurations.jni.files.collect { zipTree(it) }
    into outputDir
}

preBuild.dependsOn(unzipJniLibs)

archivesBaseName = "fxa"

apply from: '../../../publish.gradle'
ext.configurePublish(
        'org.mozilla.components',
        'fxa',
        'A library for integrating with Firefox Accounts.')
