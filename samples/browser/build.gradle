/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.build['compileSdkVersion']

    defaultConfig {
        applicationId "org.mozilla.samples.browser"
        minSdkVersion rootProject.ext.build['minSdkVersion']
        targetSdkVersion rootProject.ext.build['targetSdkVersion']
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "channel"

    productFlavors {
        nightly {
            dimension "channel"
        }
        beta {
            dimension "channel"
        }
        production {
            dimension "channel"
        }
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    implementation project(':concept-engine')
    implementation project(':concept-toolbar')

    implementation project(':browser-engine-system')


    implementation project(':browser-search')
    implementation project(':browser-session')
    implementation project(':browser-toolbar')
    implementation project(':browser-menu')

    implementation project(':feature-search')
    implementation project(':feature-session')
    implementation project(':feature-toolbar')

    implementation project(':ui-autocomplete')

    nightlyImplementation "org.mozilla:geckoview-nightly-armeabi-v7a:${rootProject.ext.geckoNightly['version']}"
    //implementation "org.mozilla:geckoview-nightly-x86:${rootProject.ext.geckoNightly['version']}"
    nightlyImplementation project(':browser-engine-gecko-nightly')

    betaImplementation "org.mozilla:geckoview-beta-armeabi-v7a:61.0.20180614135649"
    betaImplementation project(':browser-engine-gecko-beta')

    productionImplementation "org.mozilla:geckoview-release-armeabi-v7a:60.0.2"
    productionImplementation project(':browser-engine-gecko')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:${rootProject.ext.dependencies['kotlin']}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${rootProject.ext.dependencies['coroutines']}"

    implementation "com.android.support:appcompat-v7:${rootProject.ext.dependencies['supportLibraries']}"
}
