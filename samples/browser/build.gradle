/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion Config.compileSdkVersion

    defaultConfig {
        applicationId "org.mozilla.samples.browser"
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "engine", "abi"

    productFlavors {
        // GeckoView release channels
        geckoNightly {
            dimension "engine"
        }
        geckoBeta {
            dimension "engine"
        }
        geckoRelease {
            dimension "engine"
        }

        // WebView
        system {
            dimension "engine"
        }
        
        // Processor architecture
        arm {
            dimension "abi"
        }
        x86 {
            dimension "abi"
        }
        aarch64 {
            dimension "abi"
        }
        universal {
            dimension "abi"
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name == "release") {
            // This is a sample app that we are not releasing. Save some time and do not build
            // release versions.
            setIgnore(true)
        }

        def flavors = variant.flavors*.name.toString().toLowerCase()
        if (flavors.contains("system") && !flavors.contains("universal")) {
            setIgnore(true)
        }

        if (flavors.contains("gecko") && flavors.contains("universal")) {
            setIgnore(true)
        }
    }
}


configurations {
    geckoNightlyArmImplementation {}
    geckoNightlyX86Implementation {}
    geckoNightlyAarch64Implementation {}

    geckoBetaArmImplementation {}
    geckoBetaX86Implementation {}
    geckoBetaAarch64Implementation {}

    geckoReleaseArmImplementation {}
    geckoReleaseX86Implementation {}
    geckoReleaseAarch64Implementation {}
}

dependencies {
    implementation project(':concept-engine')
    implementation project(':concept-tabstray')
    implementation project(':concept-toolbar')

    implementation project(':browser-engine-system')

    implementation project(':browser-search')
    implementation project(':browser-session')
    implementation project(':browser-tabstray')
    implementation project(':browser-toolbar')
    implementation project(':browser-menu')

    implementation project(':feature-intent')
    implementation project(':feature-search')
    implementation project(':feature-session')
    implementation project(':feature-toolbar')
    implementation project(':feature-tabs')

    implementation project(':ui-autocomplete')

    implementation project(':support-utils')

    geckoNightlyImplementation project(':browser-engine-gecko-nightly')
    geckoNightlyArmImplementation Gecko.geckoview_nightly_arm
    geckoNightlyX86Implementation Gecko.geckoview_nightly_x86
    geckoNightlyAarch64Implementation Gecko.geckoview_nightly_aarch64

    geckoBetaImplementation project(':browser-engine-gecko-beta')
    geckoBetaArmImplementation "org.mozilla.geckoview:geckoview-beta-armeabi-v7a:${rootProject.ext.geckoBeta['version']}"
    geckoBetaX86Implementation "org.mozilla.geckoview:geckoview-beta-x86:${rootProject.ext.geckoBeta['version']}"
    geckoBetaAarch64Implementation "org.mozilla.geckoview:geckoview-beta-arm64-v8a:${rootProject.ext.geckoBeta['version']}"

    geckoReleaseImplementation project(':browser-engine-gecko')
    geckoReleaseArmImplementation "org.mozilla:geckoview-release-armeabi-v7a:${rootProject.ext.geckoRelease['version']}"
    geckoReleaseX86Implementation "org.mozilla:geckoview-release-x86:${rootProject.ext.geckoRelease['version']}"
    geckoReleaseAarch64Implementation "org.mozilla:geckoview-release-arm64-v8a:${rootProject.ext.geckoRelease['version']}"

    implementation Deps.kotlin_stdlib
    implementation Deps.kotlin_coroutines

    implementation Deps.support_appcompat
    implementation Deps.support_constraintlayout
}
